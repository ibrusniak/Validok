
Функция Валидация(Знач Шаблон, Знач Данные, ТекущееИмя = "") Экспорт

	МассивОшибок = Новый Массив;
	
	КубусОбщегоНазначенияКлиентСервер.ПроверитьПараметрФункции(
		"Валидация",
		"Шаблон",
		Тип("Соответствие"),
		Шаблон);
	
	КубусОбщегоНазначенияКлиентСервер.ПроверитьПараметрФункции(
		"Валидация",
		"Данные",
		Тип("Соответствие"),
		Данные);
		
	Для каждого КЗ Из Шаблон Цикл
		
		Имя = КЗ.Ключ;
		Правила = КЗ.Значение;
		
		Значение = Данные[Имя];
		
		Обязательность = Правила["Обязательность"];
		ТипЗначения = Тип(Правила["Тип"]);
		
		Если НЕ ЗначениеЗаполнено(Значение) Тогда
			
			Если Обязательность Тогда
				МассивОшибок.Добавить("Значение '" + ?(ПустаяСтрока(ТекущееИмя), "", ТекущееИмя + ".") + Имя + "' - должно быть задано обязательно (" + ТипЗначения + ").");
			КонецЕсли;
			Продолжить;
			
		КонецЕсли;
			
		Если ТипЗнч(Значение) <> ТипЗначения Тогда
			МассивОшибок.Добавить("Тип значения '" + ?(ПустаяСтрока(ТекущееИмя), "", ТекущееИмя + ".") + Имя + "' должен быть '" + ТипЗначения + "'.");
			Продолжить;
		КонецЕсли;
		
		Если ТипЗначения = Тип("Соответствие") И Правила["Шаблон"] <> Неопределено Тогда
			М = Валидация(Правила["Шаблон"], Значение, Имя);
			Если М.Количество() <> 0 Тогда
				Для каждого Элемент Из М Цикл
					МассивОшибок.Добавить(Элемент);
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
		
	Возврат МассивОшибок;
	
КонецФункции
